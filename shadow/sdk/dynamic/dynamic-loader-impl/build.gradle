apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

group 'com.tencent.shadow.dynamic'

android {
    compileSdkVersion COMPILE_SDK_VERSION

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION

        //gradle的编译版本升级到4.1之后，废弃了VERSION_CODE和VERSION_NAME，需要自己定义这两个字段了
        buildConfigField "int", 'VERSION_CODE', String.valueOf(VERSION_CODE)
        buildConfigField 'String', 'VERSION_NAME', "\"" + VERSION_NAME + "\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'plugin-loader-release.pro'
        }

        release {
            consumerProguardFiles 'plugin-loader-release.pro'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$KOTLIN_VERSION"
    testImplementation 'junit:junit:4.12'

    implementation 'com.tencent.shadow.core:loader'
    compileOnly 'com.tencent.shadow.core:activity-container'
    implementation 'com.tencent.shadow.coding:lint'
    compileOnly 'com.tencent.shadow.core:common'
    compileOnly project(':dynamic-host')
    compileOnly project(':dynamic-loader')
}

def jarPackage(buildType) {
    return tasks.create("jar${buildType.capitalize()}Package", Copy) {
        def aarFile = file(project.buildDir.path + "/outputs/aar/${project.name}-${buildType}.aar")
        def outputDir = file(project.buildDir.path + "/outputs/jar")

        from zipTree(aarFile)
        into outputDir
        include 'classes.jar'
        rename 'classes.jar', "${project.name}-${buildType}.jar"
        group = 'build'
        description = '生成jar包'
    }.dependsOn(project.getTasksByName("assemble${buildType.capitalize()}", false).first())
}

afterEvaluate {
    android {
        buildTypes.findAll().each { buildType ->
            def buildTypeName = buildType.getName()
            jarPackage(buildTypeName)
        }
    }
}
